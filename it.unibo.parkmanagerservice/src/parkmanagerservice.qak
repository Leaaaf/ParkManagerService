System carparking

// Devices
Dispatch dopolling : dopolling(X)
Dispatch stoppolling : stoppolling(X)
Dispatch fanon : fanon(X)
Dispatch fanoff : fanoff(X) 
Event weighton : weighton(X)
Event weightoff : weightoff(X)
Event outsonaron : outsonaron(X)
Event outsonaroff : outsonaroff(X)
Event criticaltemp : criticaltemp(X)

// Trolley
Dispatch parkcar : parkcar(SLOTNUM)
Dispatch pickupcar : pickupcar(SLOTNUM)
Dispatch stoptrolley : stoptrolley(X)
Dispatch resumetrolley : resumetrolley(X)

// Basicrobot
Dispatch cmd : cmd(MOVE) 
Request step : step( TIME )	
Reply stepdone : stepdone(V)
Reply stepfail : stepfail(DURATION, CAUSE)

// Counters
Dispatch startdtfree : startdtfree(X)
Dispatch startitocc : startitocc(X)
Dispatch stopcount : stopcount(X)
Event dtfreereached : dtfreereached(X)
Event itoccreached : itoccreached(X)

// Enter car
Request enter : enter(EMAIL)
Request entercar : entercar(SLOTNUM, MAIL)
Reply slotnum : slotnum(SLOTNUM)
Reply token : token(TOKEN)
// Pick-up car
Request pickup : pickup(TOKEN, MAIL)
Reply canpickup : canpickup(X)

// Notify
Dispatch notifyuser : notifyuser(X)

// Ctx carparking
Context ctxcarparking ip [host="localhost" port=8100]
// Ctx devices
Context ctxfan ip [host="localhost" port=8001]
Context ctxweightsensor ip [host="localhost" port=8002]
Context ctxthermometer ip [host="localhost" port=8003]
Context ctxoutsonarsensor ip [host="localhost" port=8004]
// Ctx basicrobot
Context ctxbasicrobot ip [host="localhost" port=8020]

ExternalQActor fanactor context ctxfan
ExternalQActor weightsensoractor context ctxweightsensor
ExternalQActor thermometeractor context ctxthermometer
ExternalQActor outsonaractor context ctxoutsonarsensor
ExternalQActor basicrobot context ctxbasicrobot

QActor parkingmanagerservice context ctxcarparking {
	[#
		it.unibo.parkingmanagerservice.repository.ParkingRepository.createParking(6)
		it.unibo.parkingmanagerservice.usecase.ParkManagerServiceUseCase.create(
			it.unibo.parkingmanagerservice.repository.ParkingRepository.getUserRepository(),
			it.unibo.parkingmanagerservice.repository.ParkingRepository.getParkingSlotRepository(),
			it.unibo.parkingmanagerservice.repository.ParkingRepository.getIndoorQueueRepository(),
			it.unibo.parkingmanagerservice.repository.ParkingRepository.getOutdoorQueueRepository(),
			it.unibo.parkingmanagerservice.repository.ParkingRepository.getDoorManagerRepository()
		)
		
		val MANAGER = it.unibo.parkingmanagerservice.usecase.ParkManagerServiceUseCase.get()
		
		// Timers
		val timers = it.unibo.parkingmanagerservice.entity.timer.Timer.get()
		val ITOCC = timers.ITOCC
		val DTFREE = timers.DTFREE
		val INDOOR_POLLING = timers.INDOOR_POLLING
		val OUTDOOR_POLLING = timers.OUTDOOR_POLLING
		
		// ParkingSlot
		var SLOTNUM : Long = 0
		var PARKING_SLOT : it.unibo.parkingmanagerservice.entity.parkingslot.ParkingSlot?
		var PARKING_SLOT_ERROR : Pair<it.unibo.parkingmanagerservice.entity.parkingslot.ParkingSlot?,
										it.unibo.parkingmanagerservice.entity.ParkingManagerServiceError?>
		
		// User - ParkingSlot
		var USER_SLOT : Pair<it.unibo.parkingmanagerservice.entity.user.User?,
								it.unibo.parkingmanagerservice.entity.parkingslot.ParkingSlot?>
							
		// User
		var USER : it.unibo.parkingmanagerservice.entity.user.User?
		var USER_ERROR : Pair<it.unibo.parkingmanagerservice.entity.user.User?,
								it.unibo.parkingmanagerservice.entity.ParkingManagerServiceError?>
						
		// Door Type
		val INDOOR = it.unibo.parkingmanagerservice.entity.door.DoorType.INDOOR
		val OUTDOOR = it.unibo.parkingmanagerservice.entity.door.DoorType.OUTDOOR
						
		// Notify
										
		var PAYLOAD : String = ""
		
	#]
	
	State s0 initial {
		println("$name | Started")
		updateResource[# "{\"door\": \"indoor\", \"state\": \"FREE\"}" #]
		updateResource[# "{\"door\": \"outdoor\", \"state\": \"FREE\"}" #]
	} Goto work
	
	State work {
		println("$name | Waiting for request...")
		updateResource[# "work" #]
	} Transition t 
		whenRequest enter -> handleEnter
		whenRequest entercar -> handleEnterCar
		whenRequest pickup -> handlePickup
		whenEvent weighton -> handleIndoorOccupied
		whenEvent weightoff -> handleIndoorFree
		whenEvent outsonaron -> handleOutdoorOccupied
		whenEvent outsonaroff -> handleOutdoorFree			
	
	State handleEnter {
		printCurrentMessage
		[# SLOTNUM = 0 #]
		onMsg (enter : enter(EMAIL)) {
			[#
				try {
					USER = MANAGER.createUser(payloadArg(0))
					SLOTNUM = MANAGER.setSlotToUser(USER!!)
					
					if (SLOTNUM > 0) {
						MANAGER.putUserReserveDoorQueue(USER!!, INDOOR)
						if (MANAGER.reserveDoor(INDOOR) != null) {
							PAYLOAD = "{\"slotnum\": \"$SLOTNUM\", \"indoor\": \"FREE\", \"time\": \"${ITOCC}\"}"
					#]
							updateResource[# "{\"door\": \"indoor\", \"state\": \"RESERVED\"}" #]
							updateResource[# "{\"slot\": \"${SLOTNUM}\", \"user\": \"${USER!!.email}\", \"state\": \"RESERVED\"}" #]
							forward weightsensoractor -m dopolling : dopolling($INDOOR_POLLING)
							forward itoccactor -m startitocc : startitocc(START)
					[# 
						} else {
							PAYLOAD = "{\"slotnum\": \"${SLOTNUM}\", \"indoor\": \"OCCUPIED\"}"
						}
					}
				} catch (e : java.sql.SQLException) {
					PAYLOAD = "{\"error\": \"${e.getLocalizedMessage()}\"}"
				}				
			#]
		}
		println("$name | Reply with slotnum: ${PAYLOAD!!}")
		replyTo enter with slotnum : slotnum($PAYLOAD)
	} Goto work
	
	State handleEnterCar {
		printCurrentMessage
		onMsg (entercar : entercar(SLOTNUM, EMAIL)) {
		[#
			USER_ERROR = MANAGER.setTokenUserIndoor(payloadArg(0), payloadArg(1))
			if (null != USER_ERROR.first && null == USER_ERROR.second) {
				PAYLOAD = "{\"token\": \"${USER_ERROR.first!!.token!!.toString()}\"}"
		#]
				forward itoccactor -m stopcount : stopcount(STOP)
				forward trolleyactor -m parkcar : parkcar($SLOTNUM)
				updateResource[# "{\"slotnum\": \"${SLOTNUM}\", \"user\": \"${USER_ERROR.first!!.email}\", \"state\": \"OCCUPIED\"}" #]
		[# 
			} else PAYLOAD = "{\"error\": \"${USER_ERROR!!.second!!.msg}\"}"
		#]	
		
		println("$name | Reply to ENTERCAR with $PAYLOAD")
		replyTo entercar with token : token($PAYLOAD)
		updateResource [# "Reply to ENTERCAR with $PAYLOAD" #]
		
		}
	} Goto work
	
	State handlePickup {
		printCurrentMessage
		onMsg (pickup : pickup(TOKEN, EMAIL)) {
			[#
				PARKING_SLOT_ERROR = MANAGER.validateToken(payloadArg(0), payloadArg(1))
				if (null != PARKING_SLOT_ERROR.first && null == PARKING_SLOT_ERROR.second) {
					SLOTNUM = PARKING_SLOT_ERROR.first!!.id
					MANAGER.putUserReserveDoorQueue(PARKING_SLOT_ERROR.first!!.user!!, OUTDOOR)
					if (null != MANAGER.reserveDoor(OUTDOOR)) {
						PAYLOAD = "{\"msg\": \"The transport trolley will transport your car to the outdoor: you will get a notification when your car is ready. Plase stay near the ourdoor\"}"
			#]
						updateResource[# "{\"slot\": \"${SLOTNUM}\", \"user\": \"${PARKING_SLOT_ERROR.first!!.user!!.email}\", \"state\": \"RELEASE\"}" #]
						forward dtfreeactor -m startdtfree : startdtfree($OUTDOOR_POLLING)
			[#			
					} else
						PAYLOAD = "{\"msg\": \"The outdoor is already engaged. When possible, the trolley will transport your car to the outdoor. You will be notified as soon.\"}"
				} else
					PAYLOAD = "{\"msg\": \"$PARKING_SLOT_ERROR.second!!\"}"
			#]
			println("$name | Reply with canpickup(${PAYLOAD!!})")
			replyTo pickup with canpickup : canpickup($PAYLOAD)
			updateResource[# "canpickup($PAYLOAD)" #]
		}
	} Goto work
	
	State handleIndoorOccupied {
		forward itoccactor -m stopcount : stopcount(STOP)
		[#
			USER = MANAGER.setDoorOccupied(INDOOR)!!	
		#]
		updateResource[# "{\"door\": \"indoor\", \"state\": \"OCCUPIED\"}" #]
	} Goto work
	
	State handleIndoorFree {
		[#
			USER = MANAGER.parkFromIndoor()
			// TODO: Send notification 
		#]
		// forward notificationacotr -m notifyuser : notifyuser(NOTIFY)
		forward weightsensoractor -m stoppolling : stoppolling(STOP)
		updateResource[# "{\"door\": \"indoor\", \"state\":\"FREE\"}" #] 
	} Goto enterNext if [# MANAGER.getReserveDoorQueue(INDOOR).getSize() > 0 #] else work
	
	State handleOutdoorOccupied {
		[#
			MANAGER.setDoorOccupied(OUTDOOR)!!
			USER_SLOT = MANAGER.setSlotFree()
			USER = USER_SLOT.first	
			
			if (null != USER!!) {
				// TODO: Send notification
		#]
			// forward notificationactor -m notifyuser : notifyuser(NOTIFY)
			forward trolleyactor -m pickupcar : pickupcar($SLOTNUM)
			updateResource[# "{\"door\": \"outdoor\", \"state\": \"OCCUPIED\"}" #]
			updateResource[# "{\"slot\": \"${USER_SLOT.second!!.id}\", \"user\": \"${USER!!.email}\", \"state\": \"FREE\"}" #]
		[#
			}
		#]
	} Goto work
	
	State handleOutdoorFree {
		[#
			MANAGER.setDoorFree(OUTDOOR)	
		#]
		updateResource[# "{\"door\": \"outdoor\", \"state\": \"FREE\"}" #]
	} Goto exitNext if [# MANAGER.getReserveDoorQueue(OUTDOOR).getSize() > 0 #] else work
	
	State enterNext {
		[#
			USER = MANAGER.reserveDoor(INDOOR)
			if (null != USER) {
				// TODO: Implement notification
		#]
				// forward notificationactor -m notifyuser : notifyuser(NOTIFY)
				forward weightsensoractor -m dopolling : dopolling($INDOOR_POLLING)
				forward itoccactor -m startitocc : startitocc(START)
				updateResource[# "{\"door\": \"indoor\", \"state\": \"RESERVED\"}" #]
				updateResource[# "{\"slot\": \"${SLOTNUM}\", \"user\": \"${USER!!.email}\",\"state\":\"RESERVED\"}" #]
		[# 
			}	
		#]
	} Goto work
	
	State exitNext {
		[#
			USER = MANAGER.reserveDoor(OUTDOOR)
			if (null != USER) {
		#]
				forward outsonaractor -m dopolling : dopolling($OUTDOOR_POLLING)
				forward dtfreeactor -m startdtfree : startdtfree(START)
				updateResource[# "{\"door\": \"outdoor\", \"state\": \"RESERVED\"}" #]
				updateResource[# "{\"slot\": \"${SLOTNUM}\", \"user\": \"${USER!!.email}\", \"state\": \"RELEASE\"}" #]
		[#
			}	
		#]
	} Goto work
}

QActor itoccactor context ctxcarparking {
	[# 	
		val ITOCC = it.unibo.parkingmanagerservice.entity.timer.Timer.get().ITOCC
		var REACHED = false
	#]
	
	State s0 initial {
		println("$name | Started...")
	} Goto work
	
	State work {
		[# 
			if(REACHED) {
				REACHED = false
		#]
			updateResource [# "reached"#]
		[# } else { #]
			updateResource [# "work"#]
		[# } #]
		println("$name | Waiting for command")
	} Transition t
		whenMsg startitocc -> count
		whenMsg stopcount -> work
	
	State count {
		println("$name | Start ITOCC count...")
		updateResource [# "count"#]
	} Transition t
		whenTimeVar ITOCC -> reached
		whenMsg startitocc -> count
		whenMsg stopcount -> work
		
	State reached {
		println("$name | ITOCC reached")
		emit itoccreached : itoccreached(REACHED)
		updateResource[# "reached" #]
	} Goto work
}

QActor dtfreeactor context ctxcarparking {
	[#
		val DTFREE = it.unibo.parkingmanagerservice.entity.timer.Timer.get().DTFREE
		var REACHED = false
	#]
	
	State s0 initial {
		println("$name | Started...")
	} Goto work
	
	State work {
		[# 
			if(REACHED) {
				REACHED = false
		#]
			updateResource [# "reached"#]
		[# } else { #]
			updateResource [# "work"#]
		[# } #]
		println("$name | Waiting for command")
	} Transition t
		whenMsg startdtfree -> count
		whenMsg stopcount -> work
		
	State count {
		println("$name | Start DTFREE count...")
		updateResource[# "count" #]
	} Transition t
		whenTimeVar DTFREE -> reached
		whenMsg startdtfree -> count
		whenMsg stopcount -> work
	
	State reached {
		println("$name | DTFREE reached")
		emit dtfreereached : dtfreereached(REACHED)
		updateResource[# "reached" #]
	} Goto work
}

QActor parkingservicestatusguiactor context ctxcarparking {
	State s0 initial {
		println("$name | Started...")
	} Goto work
	
	State work {
		println("$name | Working...")
	} Transition t
		whenEvent dtfreereached -> dtfreealarm
	
	State dtfreealarm {
		println("$name | Notification sent to manager: DTFREE reached")
	} Goto work
}

QActor temperatureactor context ctxcarparking {
	[# var type : String #]
	
	State s0 initial {
		println("$name | Started...")
	} Goto work
	
	State work {
		println("$name | Working...")
	} Transition t
		whenEvent criticaltemp -> handlecriticaltemp
		
	State handlecriticaltemp {
		onMsg (criticaltemp : criticaltemp(X)) {
			[#
				type = payloadArg(0)
				if (type.toUpperCase().equals("CRITICAL")) {
			#]
				println("$name | Critical temp reached")
				forward fanactor -m fanon : fanon(ON)
			[#
				} else if (type.toUpperCase().equals("NORMAL")) {	
			#]
				println("$name | Normal temp reached")
				forward fanactor -m fanon : fanon(ON)
			[#
				}	
			#]
		}
	} Goto work
}

QActor trolleyactor context ctxcarparking {
	[#
		it.unibo.parkingmanagerservice.utility.MapLoader.loadMapFromTxt("resources/parking_map.txt")
		val STEP_TIME = "340"
		val PLANNER = itunibo.planner.plannerUtil
		val PARKING_SLOT_MAP = it.unibo.parkingmanagerservice.repository.parkingslot.ParkingSlotMap
		val DOOR_MAP = it.unibo.parkingmanagerservice.repository.door.DoorMap
		val INDOOR = it.unibo.parkingmanagerservice.entity.door.DoorType.INDOOR
		val OUTDOOR = it.unibo.parkingmanagerservice.entity.door.DoorType.OUTDOOR
		val IN_POSITION = DOOR_MAP.getAdiacentAllowedPositionByDoorType(INDOOR)!!
		val OUT_POSITION = DOOR_MAP.getAdiacentAllowedPositionByDoorType(OUTDOOR)!!
		var DEST : Pair<Int, Int>? = null
		var TRIP_BUILDER = it.unibo.parkingmanagerservice.utility.TripBuilder(IN_POSITION.first, IN_POSITION.second, OUT_POSITION.first, OUT_POSITION.second)
		var TRIP : kotlin.collections.Iterator<it.unibo.parkingmanagerservice.entity.trolley.TripStage>? = null
		var PLAN : kotlin.collections.Iterator<aima.core.agent.Action>? = null
		var ACTION : aima.core.agent.Action? = null
		var CURR_STAGE : it.unibo.parkingmanagerservice.entity.trolley.TripStage? = null
		var CAN_GO = false
		var STATE = it.unibo.parkingmanagerservice.entity.trolley.TrolleyState.IDLE
		var INTERRUPT = false
		var EXPECTED_DIRECTION : String? = null
		var PAYLOAD = ""
		
		PLANNER.initAI()
		PLANNER.showMap()
	#]
	
	State s0 initial {
		println("$name | Started...")
	} Goto home
	
	State home {
		println("$name | Home")
		[#
			TRIP = null
			PLAN = null
			CURR_STAGE = null
			STATE = `it.unibo.parkingmanagerservice.entity.trolley`.TrolleyState.IDLE	
			
			PLANNER.showMap()
			PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE?.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
		#]
		updateResource[# PAYLOAD #]
	} Transition t 
		whenMsg parkcar -> handle
		whenMsg pickupcar -> handle
		whenMsg stoptrolley -> stop
		
	State handle {
		[#
			STATE = `it.unibo.parkingmanagerservice.entity.trolley`.TrolleyState.WORKING 
			PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE?.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
			TRIP_BUILDER.clear()
		#]
		updateResource[# PAYLOAD #]
		
		onMsg (parkcar : parkcar(SLOTNUM)) {
			println("$name | Received request to park the car at the indoor to the slot ${payloadArg(0)}")
			[# 
				DEST = PARKING_SLOT_MAP.getAdiacentAllowedPositionBySlotnum(payloadArg(0))
				println("$name | Found slot at coordinates $DEST")
				if(DEST != null) {
					TRIP = TRIP_BUILDER.addParkTrip(DEST!!.first, DEST!!.second).build()
				} else
					println("$name | Unable to find a plan to go to the slot ${payloadArg(0)}")
			#]
		}
		
		onMsg (pickupcar : pickupcar(SLOTNUM)) {
			println("$name | Received request to pickup the car at the slot ${payloadArg(0)}")
			[# 
				DEST = PARKING_SLOT_MAP.getAdiacentAllowedPositionBySlotnum(payloadArg(0))
				println("$name | Found slot at coordinates $DEST")
				if(DEST != null) {	
					TRIP = TRIP_BUILDER.addPickupTrip(DEST!!.first, DEST!!.second).build()
				} else
					println("$name | Unable to find a plan to go to the slot ${payloadArg(0)}")
			#]
		}
	} Goto nextstage if [# null != DEST #] else home

	State nextstage {
		[# 
			CAN_GO = false
			if(TRIP != null) {
				while(TRIP!!.hasNext() && !CAN_GO) {
					CURR_STAGE = TRIP!!.next()
					println("$name ! CURRSTAGE = ${CURR_STAGE}")
					INTERRUPT = false
					
					when(CURR_STAGE!!.type) {
						`it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.LOAD_CAR -> {
							CAN_GO = true
						}
						`it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.UNLOAD_CAR -> {
							CAN_GO = true
						}
						else -> {
							PLANNER.setGoal(CURR_STAGE!!.destination.first, CURR_STAGE!!.destination.second)
							PLAN = PLANNER.doPlan()?.iterator()
							if(PLAN != null) {
								CAN_GO = true
							}
							if(CURR_STAGE!!.type == `it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.MOVING_TO_HOME) {
								INTERRUPT = true
							} else {
								INTERRUPT = false
							}
						}
					}
				}	
			}
		#]
	} Goto checkstage if [# CAN_GO #] else home
	
	State checkstage {
		println("$name | Check stage")
	} Goto elevator if [# CURR_STAGE!!.type == `it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.LOAD_CAR ||
			CURR_STAGE!!.type == `it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.UNLOAD_CAR #] else nextaction
			
	State elevator {
		[# 
			EXPECTED_DIRECTION = DOOR_MAP.getDirection(PLANNER.getPosX(), PLANNER.getPosY()) ?: PARKING_SLOT_MAP.getDirection(PLANNER.getPosX(), PLANNER.getPosY())
			if(EXPECTED_DIRECTION != null) {
				while(EXPECTED_DIRECTION != PLANNER.getDirection()) {
					#] 
						forward basicrobot -m cmd : cmd(l)
						delay 500
					[#
						PLANNER.doMove("l")
				}
			}
			
			when(CURR_STAGE!!.type) {
				`it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.LOAD_CAR -> {
					println("$name | Car will be loaded")
					PLANNER.showMap()
					PLANNER.resetActions()
					PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE!!.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
					#] 
						updateResource[# PAYLOAD #]
						delay 3000
					[#
					println("$name | Car is loaded")
				}
				`it.unibo.parkingmanagerservice.entity.trolley`.TripStageType.UNLOAD_CAR-> {
					println("$name | Car will be unloaded")
					PLANNER.showMap()
					PLANNER.resetActions()
					PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE!!.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
					#] 
						updateResource[# PAYLOAD #]
						delay 3000
					[#
					println("$name | Car is unloaded")
				}
				else -> {}
			}
		#]
	} Goto nextstage

	State nextaction {
		[#
			CAN_GO = false
			if (null != TRIP && PLAN!!.hasNext()) {
				ACTION = PLAN!!.next()
				
				if (null != ACTION && !ACTION!!.isNoOp) {
					CAN_GO = true
				}
			}	
		#]
	} Goto doaction if [# CAN_GO #] else nextstage
	
	State doaction {
		[#
			CAN_GO = false
			if (ACTION.toString().equals("w")) {
		#]
			request basicrobot -m step : step($STEP_TIME)
		[#
			CAN_GO = true
			} else {
		#]
			forward basicrobot -m cmd : cmd($ACTION)
			delay 500
		[#
			PLANNER!!.doMove(ACTION.toString())
			PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE!!.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
		#]
			updateResource[# PAYLOAD #]
		[#	
			}
		#]
	} Goto waitstepdone if [# CAN_GO #] else nextaction
	
	State waitstepdone {
		println("$name | Waiting step done...")
	} Transition t
		whenMsg stoptrolley -> stop
		whenMsg parkcar and [# INTERRUPT #] -> handle
		whenMsg pickupcar and [# INTERRUPT #] -> handle
		whenReply stepdone -> handlestepdone
		
	State handlestepdone {
		[#
			PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE!!.type}\", \"position\": {\"x\": \"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
		#]
		updateResource[# PAYLOAD #]
	} Goto nextaction

	State stop {
		onMsg (stepdone : stepdone(V)) {
			[# PLANNER!!.doMove(ACTION.toString()) #]
		} 
		[#
			STATE = `it.unibo.parkingmanagerservice.entity.trolley`.TrolleyState.STOPPED
			PAYLOAD = "{\"state\": \"${STATE}\", \"action\": \"${CURR_STAGE?.type}\", \"position\": {\"x\":\"${PLANNER.getPosX()}\", \"y\": \"${PLANNER.getPosY()}\"}}"
		#]
		updateResource[# PAYLOAD #]
	} Transition t
		whenMsg resumetrolley -> nextaction
		whenReply stepdone -> stop
}





