System fan

Dispatch updatefan : updatefan(X)
Dispatch fanon : fanon(X)
Dispatch fanoff : fanoff(X)

Context ctxfan ip [host="localhost" port=8001]

QActor fanactor context ctxfan {
	[#
		val fan = it.unibo.devices.DeviceManager.getDevice("fan")
		lateinit var state : it.unibo.fan.FanState
		
		if (fan == null) {
			println("$name | Unable to use the fan")
			System.exit(-1)
		}
		
		fan as it.unibo.fan.AbstractFan	
	#]
	
	State s0 initial {
		println("$name | Started...")	
		[#
			state = `it.unibo.fan`.FanState.OFF
		#]
	} Goto work
	
	State work {
		updateResource[# state.toString() #]
		println("$name | Fan state: ${state.toString()}")
	} Transition t0 whenMsg updatefan 	-> work
					whenMsg fanon 		-> poweron
					whenMsg fanoff 		-> poweroff
		
	State poweron {
		[#
			fan.powerOn()
			state = `it.unibo.fan`.FanState.ON
		#]
	} Goto work
	
	State poweroff {
		[#
			fan.powerOff()
			state = `it.unibo.fan`.FanState.OFF
		#]
	} Goto work
	
}